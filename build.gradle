plugins {
    id 'org.springframework.boot' version '2.4.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.6.0'
    id "org.sonarqube" version "3.3"
}

group = 'br.com.poatransporte'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    compile 'org.flywaydb:flyway-core'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    runtimeOnly 'io.r2dbc:r2dbc-postgresql'
    runtimeOnly 'org.postgresql:postgresql'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

check { dependsOn jacocoTestCoverageVerification }
build { dependsOn 'pitest' }
jacoco { toolVersion = "0.8.6" }

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.2
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'br/com/poatransporte/config/**',
                    'br/com/poatransporte/PoaTransporteApplication.class'
            ])
        })
    }
}

pitest {
    junit5PluginVersion = '0.12'
    timestampedReports = false
    mutationThreshold = 50
    excludedClasses = ['br.com.poatransporte.config.*', 'br.com.poatransporte.PoaTransporteApplication']
}

sonarqube {
  properties {
    property "sonar.projectKey", "duartevinicius91_poatransporte"
    property "sonar.organization", "duartevinicius91"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}
